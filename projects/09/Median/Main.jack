// Inputs some numbers and computes their median 
class Main {
   function void main() {
      var Array a;
      var int length, sum;
      var int i, j, k, t, n;

      let length = Keyboard.readInt("How many numbers? ");
      let a = Array.new(length); // constructs the array

      let i = 0;
      while (i < length) {
        let a[i] = Keyboard.readInt("Enter a number: ");
        let i = i + 1;
      }

      // Sort
      let j = 1;
      while (j < (length + 1)) {
        let k = 1;
        while (k < (length - j + 1)) {
          if (a[k] < a[k - 1]) {
            let t = a[k];
            let a[k] = a[k - 1];
            let a[k - 1] = t;
          }
          let k = k + 1;
        }
        let j = j + 1;
      }

      // Median
      let n = length / 2;
      do Output.printString("The median is ");
      if (n * 2 = length) {
        do Output.printInt((a[n - 1] + a[n]) / 2);
      }
      else {
        do Output.printInt(a[n]);
      }
      return;
   }
}

